#pragma kernel Polarize

Texture2D<float4> InputTex;
RWTexture2D<float4> OutputTex;

SamplerState SmpPointRepeat;
SamplerState SmpPointMirror;
SamplerState SmpLinearRepeat;
SamplerState SmpLinearMirror;

uint width;
uint height;

#define PI 3.14159265f
#define TAU 6.2831853f

/* Rotates a coordinate xy about a center point by theta degrees*/
float2 rotate(float2 xy, float2 center, float theta)
{
    float r = distance(center, xy);
    float startAngle = atan2(xy.y - center.y, xy.x - center.x);
    float newX = center.x + r * cos(theta + startAngle);
    float newY = center.y + r * sin(theta + startAngle);
    return float2(newX, newY);
}

/* Transforms a pixel coordinate in 151x96 canopy space, where 
 * x in [0,150] specifies distance from the center along a strip, and
 * y in [0,95] specifies the strip's position around the circle, to
 * the texture coordinate such that the physical pixel matches
 * its position in a standard 2D layout, using the Canopy's physical
 * parameters to inform the transformation. */
float2 canopyDepolarize(uint2 pixel, float2 dimensions) {
    float2 center = float2(0, 0);
    float apexRadius = 0.332f;    // Radius of the dark center in meters
    float stripLength = 2.5f;     // Length of the strips in meters
    float normalizedStripAngle = pixel.y / 96.0f;  // Strip angle around center in [0-1) 
                                                   // 0 and 2pi are the same angle so we don't fully cover [0-1]
    float normalizedPixelStripPosition = pixel.x / 151.0f; // position along strip in [0-1]
    float normalizedX = ((stripLength*normalizedPixelStripPosition) + apexRadius) / (stripLength + apexRadius);
    float2 rotatedPixel = rotate(float2(normalizedX, 0), center, normalizedStripAngle * TAU);
    float normalizedDimension = min(dimensions.x, dimensions.y);
    return (normalizedDimension*rotatedPixel / 2) + (normalizedDimension / 2);
}

[numthreads(25, 16, 1)]
void Polarize(uint3 id : SV_DispatchThreadID)
{
    float2 maxDimensions = uint2(width - 1, height - 1);
    float2 coord = canopyDepolarize(id.xy, float2(width, height));
    float4 result = InputTex.SampleLevel(SmpPointRepeat, coord/maxDimensions, 0);
    OutputTex[id.xy] = result;
}